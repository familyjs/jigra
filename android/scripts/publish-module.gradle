apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    dependsOn(':bundleReleaseAar')
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = 'com.jigrajs'
version = JIG_LIB_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
//                from(components.java)

                // Coordinates
                groupId 'com.jigrajs'
                artifactId 'core'
                version JIG_LIB_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // POM Data
                pom {
                    name = 'core'
                    description = 'Jigra Android Core Native Library'
                    url = 'https://github.com/familyjs/jigra'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/familyjs/jigra/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'Family'
                            email = 'nkduy.dev@gmail.com'
                        }
                    }

                    // Version Control Info
                    scm {
                        connection = 'scm:git:github.com:familyjs/jigra.git'
                        developerConnection = 'scm:git:ssh://github.com:familyjs/jigra.git'
                        url = 'https://github.com/familyjs/jigra/tree/main'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/familyjs/jigra")
                credentials {
                    username = JIG_AUTHOR
                    password = JIG_RELEASE_TOKEN
                }
            }
        }
    }
}
